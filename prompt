#!/bin/zsh

###################
# Vi mode indicator
function zle-line-init zle-keymap-select {
  VIM_PROMPT="%{$fg_bold[yellow]%}[% NORMAL]% %{$reset_color%}"
  RPS1="${${KEYMAP/vicmd/$VIM_PROMPT}/(main|viins)/}"
  zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select
###################

autoload -U colors && colors
setopt promptsubst
git_br() {
  ref=$(git symbolic-ref HEAD 2> /dev/null) || ref=$(git rev-parse --short HEAD 2> /dev/null) || return
  if [[ -n $(git status -s 2> /dev/null) ]]
  then
    echo "%{$fg[magenta]%}${ref#refs/heads/} "
  else
    echo "%{$fg[green]%}${ref#refs/heads/} "
  fi
}
local user='%{$fg[green]%}%n%{$reset_color%}'
local host='%{$fg[green]%}%m%{$reset_color%}'
local gitbr='$(git_br)'
local pwd='%{$fg[blue]%}%c%{$reset_color%}'
local last_cmd_status='%(?.%{$fg[blue]%}.%{$fg[red]%})%%%{$reset_color%}'
# local last_char='%{$fg[blue]%}%%%{$reset_color%} '
PROMPT="${user}@${host}:${pwd} ${gitbr}${last_cmd_status} "
